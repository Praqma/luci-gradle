buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.2"
}


apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'

group 'net.praqma.luci'
version = '0.4-SNAPSHOT'
ext {
    artifact = 'luci-gradlePlugin'
    pluginId = 'net.praqma.luci'
    coreVersion = version
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile (findProject(':luci-core') ?: "net.praqma.luci:luci-core:${coreVersion}")
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourceJar

            artifactId artifact
        }
    }
}

bintray {
    user = project.properties.bintrayUser
    key = project.properties.bintrayApiKey
    publications = ['plugin']
    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'gradle-plugins'
        name = "${group}:${artifact}"
        userOrg = 'praqma'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Praqma/luci-gradle.git'
        publicDownloadNumbers = true
        version {
            attributes = ['gradle-plugin': "${pluginId}:${group}:${artifact}"]
        }
    }
}


pluginBundle {
    website = 'https://github.com/Praqma/luci-gradle'
    vcsUrl = 'https://github.com/Praqma/luci-gradle.git'
    description = 'Gradle configuration for Luci. Luci Understands Continuous Integration'
    tags = ['luci']

    plugins {
        greetingsPlugin {
            id = 'net.praqma.luci'
            displayName = 'Luci with Gradle'
        }
    }
}
