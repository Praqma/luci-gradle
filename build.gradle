buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.2"
}


apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'

group 'net.praqma.luci'
version = '0.4.1'
ext {
    artifact = 'luci-gradlePlugin'   // The maven artifact name
    pluginId = 'net.praqma.luci'     // The Gradle plugin id
    coreVersion = version            // Version of dependency luci-core
}

repositories {
    jcenter()
}

dependencies {
    // If 'luci-core' is found as project in multiproject build use project dependency
    // otherwise depend on the jar
    compile (findProject(':luci-core') ?: "net.praqma.luci:luci-core:${coreVersion}")
    compile 'org.codehaus.gpars:gpars:1.2.1'
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourceJar

            artifactId artifact
        }
    }
}

bintray {
    user = project.properties.bintrayUser
    key = project.properties.bintrayApiKey
    publications = ['plugin']
    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'gradle-plugins'
        name = "${group}:${artifact}"
        userOrg = 'praqma'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Praqma/luci-gradle.git'
        publicDownloadNumbers = true
        version {
            attributes = ['gradle-plugin': "${pluginId}:${group}:${artifact}"]
        }
    }
}


pluginBundle {
    website = 'https://github.com/Praqma/luci-gradle'
    vcsUrl = 'https://github.com/Praqma/luci-gradle.git'
    description = 'Gradle configuration for Luci. Luci Understands Continuous Integration'
    tags = ['luci']

    plugins {
        greetingsPlugin {
            id = 'net.praqma.luci'
            displayName = 'Luci with Gradle'
        }
    }
}
